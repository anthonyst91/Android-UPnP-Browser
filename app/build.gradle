apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode rootProject.versionCode
        versionName rootProject.versionName
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles 'proguard-rules.pro'
        }
        mock {
            minifyEnabled false
            debuggable true
            multiDexEnabled true
        }
    }

    flavorDimensions "printoid"

    productFlavors {
        phonesPremium {
            dimension "printoid"
        }
        phones {
            dimension "printoid"
        }
        phonesTrial {
            dimension "printoid"
        }
    }

    android.variantFilter { variant ->
        def flavorName = variant.getFlavors().get(0).name
        def isTypeMock = variant.buildType.name == 'mock'
        def isTypeDebug = variant.buildType.name == 'debug'

        // Exclude variants for phonesPremium
        if (flavorName == 'phonesPremium' && isTypeDebug) variant.setIgnore(true)

        // Exclude variants for phones
        if (flavorName == 'phones' && isTypeDebug) variant.setIgnore(true)

        // Exclude variants for phonesTrial
        if (flavorName == 'phonesTrial' && isTypeMock) variant.setIgnore(true)
        if (flavorName == 'phonesTrial' && isTypeDebug) variant.setIgnore(true)
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AppCompat
    implementation "androidx.appcompat:appcompat:$ax_appcompat_version"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:$ax_recyclerview_version"

    // EventBus -> https://github.com/greenrobot/EventBus
    implementation "org.greenrobot:eventbus:$eventbus_version"

    // okhttp3 -> http://square.github.io/okhttp
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp3_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"

    // RX Android -> https://github.com/ReactiveX/RxAndroid
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    // Picasso -> https://github.com/square/picasso
    implementation "com.squareup.picasso:picasso:2.71828"
}
